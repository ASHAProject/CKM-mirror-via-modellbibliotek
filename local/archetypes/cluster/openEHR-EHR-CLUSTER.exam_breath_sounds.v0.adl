archetype (adl_version=1.4; uid=03efffcb-c798-4ff3-b1a8-6176cb8f6aa7)
	openEHR-EHR-CLUSTER.exam_breath_sounds.v0

concept
	[at0000]	-- Examination of the breath sounds
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-06-22">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording a narrative description and clinical interpretation of the findings observed during the physical examination of the breath sounds.">
			use = <"Use to record a narrative description of the overall findings observed during the physical examination of the breath sounds. 

Insert the CLUSTER.exclusion_exam archetype in the 'Examination not done' SLOT to optionally record explicit details about the examination not being performed.

Use to incorporate the narrative descriptions of clinical findings within existing or legacy clinical systems into an archetyped format, using the 'Clinical Description' data element.">
			misuse = <"Not to be used for recording data not related to the physical examination of the breath sounds.

Not to be used for recording the clinical history - use specific OBSERVATION and CLUSTER archetypes. For example OBSERVATION.story and CLUSTER.symptom.

Not to be used to record stand-alone clinical observations or measurements or test results - use specific OBSERVATION archetypes. For example OBSERVATION.blood_pressure or OBSERVATION.imaging_exam.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Bergen Hospital Trust, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom", "John Tore Valand, Haukeland Universitetssjukehus, Norway", "Heather Leslie, Ocean Informatics, Australia">
	other_details = <
		["revision"] = <"0.0.1-alpha">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["build_uid"] = <"a56c35b2-d341-42e0-90b0-269f81b4d3f1">
		["original_publisher"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"4EB5875D8935B9F64D3F86A02116CDA5">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	CLUSTER[at0000] matches {	-- Examination of the breath sounds
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0009] occurrences matches {0..1} matches {	-- Lung examined
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0010, 	-- Left
							at0011]	-- Right
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- No abnormality detected
				value matches {
					DV_BOOLEAN matches {
						value matches {True}
					}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Examination findings
				include
					archetype_id/value matches {/.*/}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Clinical interpretation
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {	-- Examination not done
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.exclusion_exam(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination of the breath sounds">
					description = <"Findings observed during the physical examination of the breath sounds.">
				>
				["at0002"] = <
					text = <"No abnormality detected">
					description = <"Statement that no abnormality was detected (NAD) on physical examination.">
					comment = <"Record as True if no abnormality was detected on examination. Specific statements about the examination can be included in the 'Clinical Interpretation' data element.">
				>
				["at0003"] = <
					text = <"Clinical description">
					description = <"Narrative description of the overall findings observed during the physical examination.">
				>
				["at0004"] = <
					text = <"Examination findings">
					description = <"Structured details about the physical examination findings.">
				>
				["at0005"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the physical examination findings.">
				>
				["at0006"] = <
					text = <"Clinical interpretation">
					description = <"Single word, phrase or brief description that represents the clinical meaning and significance of the physical examination findings.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the physical examination findings, not captured in other fields.">
				>
				["at0008"] = <
					text = <"Examination not done">
					description = <"Details to explicitly record that this examination was not performed.">
				>
				["at0009"] = <
					text = <"Lung examined">
					description = <"Identification of the the lung under examination.">
				>
				["at0010"] = <
					text = <"Left">
					description = <"The left lung was examined.">
				>
				["at0011"] = <
					text = <"Right">
					description = <"The right lung was examined.">
				>
			>
		>
	>
