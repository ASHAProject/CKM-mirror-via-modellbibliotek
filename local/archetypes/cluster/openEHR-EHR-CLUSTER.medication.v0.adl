archetype (adl_version=1.4; uid=0a59f593-d34c-4e13-8a74-0237c8ec5b1d)
	openEHR-EHR-CLUSTER.medication.v0

concept
	[at0000]	-- Medication
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand, Gro Hilde Ulriksen, Line Silsand">
				["organisation"] = <"Helse Bergen Hospital Trust, Norwegian Centre for E-health Research">
			>
		>
		["pt"] = <
			language = <[ISO_639-1::pt]>
			author = <
				["name"] = <"Beatriz de Faria Leao">
				["organisation"] = <"Bleao Informática em Saúde">
				["email"] = <"bfleao@terra.com.br">
			>
			accreditation = <"MD, PhD">
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"NEHTA">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2015-10-21">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere detaljer om et legemiddel eller en komponent av et legemiddel, inkludert detaljer om spesifikke bestanddeler.">
			use = <"For å registrere detaljer om et legemiddel eller en komponent av et legemiddel, inkludert detaljer om spesifikke bestanddeler.
Brukes for å registrere detaljer tilberedningen av et legemiddel/preparat en legemiddelordinering (INSTRUCTION.medication) som inneholder forskriverens hensikt med ordineringen, eller i en legemiddelhandling (ACTION.medication) som dokumenterer hvilket preparatet som faktisk ble administrert, utdelt eller ekspedert.
De fleste ordineringer trenger ikke denne arketypen. For generiske ordineringer vil det spesifikke produktet bestemmes av de lokale legemiddelavtalene eller varene apoteket har tilgjengelig. Der det ordineres et produkt, vil legemiddelnavnet inneholde detaljer om form og styrke av produktet.">
			keywords = <"legemiddel", "tilbereding", "ordinering", "forskrive", "terapi", "substans", "medikament", "terapeutisk", "medikamenter solgt over disk", "vare", "ad-hoc", "magistrell", "ex tempore", "improvisert">
			misuse = <"Brukes ikke for å få oversikt over varelageret på et apotek.">
		>
		["pt"] = <
			language = <[ISO_639-1::pt]>
			purpose = <"Para registrar detalhes de uma preparação de medicamentos, incluindo, quando necessário, detalhes de múltiplos ingredientes, no contexto de uma infusão ou preparação ad-hoc (manipulada). A maioria das prescrições baseadas em dose terá sua precisa preparação determinada pelo estoque disponível na unidade, ou pelo fornecimento de farmácia, enquanto que, com a prescrição baseada em produtos, o nome da medicação traz detalhes da forma e concentração da preparação.">
			use = <"Para registrar os detalhes da preparação de um medicamento prescrito na INSTRUCTION de medicamentos, obedecendo a intenção do prescritor, no momento da execução da prescrição registrada pela ACTION de medicamentos, anotando a preparação efetivamente fornecida.">
			keywords = <"medicação", "ordem", "prescrever", "terapia", "substância", "droga", "terapêutico", "venda livre", "ítem terapêutico", "Ad hoc", "manipulados">
			misuse = <"Utilização em controle de estoque de farmácias está fora do escopo deste arquétipo.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details of a medication or component of a medication, including details of any specific constituents.">
			use = <"For recording details of a medication or component of a medication, including details of any specific constituents.

This archetype is intended to be used within the context of the original medication order INSTRUCTION and carrying the prescriber's intent, or in the context of a medication ACTION where it serves as a record of the preparation actually supplied or administered.

For the majority of precscriptions, this archetype is not required. Dose-based prescriptions will have their precise preparation determined by available ward stock, or by pharmacy supply, while with product-based prescribing, the medication name carries details of the form and strength of the preparation.">
			keywords = <"medication", "order", "prescribe", "therapy", "substance", "drug", "therapeutic", "otc", "therapeutic good", "ad-hoc", "extemperaneous">
			misuse = <"Use in pharmacy stock-control is out-of-scope of the design of this archetype.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "John Bennett, NEHTA, Australia", "SB Bhattacharyya, Sudisa Consultancy Services, India", "Sharmila Biswas, Australia", "Laila Bruun, Oslo universitetssykehus HF, Norway", "Stephen Chu, NEHTA, Australia (Editor)", "Matthew Cordell, NEHTA, Australia", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Gail Easterbrook, Flinders Medical Centre, Australia", "Stig Erik Hegrestad, Helse Førde, Norway", "David Evans, Queensland Health, Australia", "Sarah Gaunt, NEHTA, Australia", "Heather Grain, Llewelyn Grain Informatics, Australia", "Trina Gregory, cpc, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Hilde Hollås, DIPS AS, Norway", "Roar Holm, Helse Vest IKT A/S, Norway", "Alfred Honore, Haukeland, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Tom Jarl Jakobsen, Helse Bergen, Norway", "Mary Kelaher, NEHTA, Australia", "Nils Kolstrup, Skansen Legekontor og Nasjonalt Senter for samhandling og telemedisin, Norway", "Harmony Kosola, Alberta Health Services, Canada", "Robert L'egan, NEHTA, Australia", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "Mark Luciani, Gloucestershire Hospital NHS Foundation Trust, United Kingdom", "Siv Marie Lien, DIPS ASA, Norway", "James McClay, University of Nebraska Medical Center, United States", "Susan McIndoe, Royal District Nursing Service, Australia", "David McKillop, NEHTA, Australia", "Hildegard McNicoll, freshEHR Clinical Informatics Ltd., United Kingdom", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Chris Mitchell, RACGP, Australia", "Stewart Morrison, NEHTA, Australia", "Andrej Orel, Marand d.o.o., Slovenia", "Chris Pearce, Melbourne East GP Network, Australia", "Camilla Preeston, Royal Australian College of General Practitioners, Australia", "Margaret Prichard, NEHTA, Australia", "Cathy Richardson, NEHTA, Australia", "Robyn Richards, NEHTA - Clinical Terminology, Australia", "Anoop Shah, University College London, United Kingdom", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Trine Strand, Oslo Universitetssykehus (OUS), Norway", "John Taylor, NEHTA, Australia", "Rowan Thomas, St. Vincent's Hospital Melbourne, Australia", "Richard Townley-O'Neill, NEHTA, Australia (Editor)", "Gro-Hilde Ulriksen, Norwegian center for ehealthresearch, Norway", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)", "Pelle Viana Lindén, Capio, Sweden", "Kylie Young, The Royal Australian College of General Practitioners, Australia">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Medication statement, [Internet]. Intermountain Healthcare. Available from http://www.opencem.org/#/20170613/Intermountain/MedicationStatement

Messaging Implementation Manual (GP2GP messages) [Internet], NHS Digital England. Available from: https://data.developer.nhs.uk/dms/mim/6.3.01/Domains/GP2GP/Document%20files/GP2GP%20IM.htm#_Toc_Section_7.2

Standards for the clinical structure and content of patient records (Complete document) [Internet]. PRSB London. [cited 2017 Aug 29]. Available from: http://theprsb.org/download/document/7-standards-for-the-clinical-structure-and-content-of-patient-records\"

Medication - FHIR v3.0.1. (2017). Retrieved 12 April 2018, from https://www.hl7.org/fhir/medication.html">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"96176E493AD14C49BDD6D74F78865186">
		["build_uid"] = <"d26070f4-ea66-468b-81d8-97c4629a8274">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] occurrences matches {0..1} matches {	-- Medication
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0132] occurrences matches {0..1} matches {	-- Component name
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0071] occurrences matches {0..*} matches {	-- Form
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0142] occurrences matches {0..1} matches {	-- Category
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0143, 	-- Ad-hoc mixture
							at0146, 	-- Ingredient
							at0144, 	-- Combination product
							at0145, 	-- Single-substance product
							at0001]	-- Combination pack
						}
					}
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0160] occurrences matches {0..1} matches {	-- Strength
				items cardinality matches {1; unordered} matches {
					ELEMENT[at0115] occurrences matches {0..1} matches {	-- Strength as concentration
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::119]>
							>
						}
					}
					ELEMENT[at0159] occurrences matches {0..1} matches {	-- Strength as mass
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::124]>
							>
						}
					}
				}
			}
			CLUSTER[at0152] occurrences matches {0..1} matches {	-- Mixture strength
				items cardinality matches {2; unordered} matches {
					ELEMENT[at0153] matches {	-- Substance amount
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::118]>
							>
						}
					}
					ELEMENT[at0157] matches {	-- Diluent amount
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::118]>
							>
						}
					}
				}
			}
			ELEMENT[at0151] occurrences matches {0..1} matches {	-- Manufacturer
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0150] occurrences matches {0..1} matches {	-- Batch ID
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Expiry
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			CLUSTER[at0126] occurrences matches {0..*} matches {	-- Constituent
				items cardinality matches {1..*; unordered} matches {
					allow_archetype CLUSTER[at0138] occurrences matches {0..1} matches {	-- Constituent details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.medication_substance(-[a-zA-Z0-9_]+)*\.v0/}
					}
					ELEMENT[at0139] occurrences matches {0..1} matches {	-- Constituent amount
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::118]>
							>
						}
					}
					ELEMENT[at0148] occurrences matches {0..1} matches {	-- Alternate constituent amount
						value matches {
							C_DV_QUANTITY <
							>
						}
					}
					ELEMENT[at0127] occurrences matches {0..1} matches {	-- Role
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0080, 	-- Therapeutic
									at0083, 	-- Adjuvant
									at0084]	-- Excipient
								}
							}
							DV_TEXT matches {*}
						}
					}
				}
			}
			ELEMENT[at0133] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0141] occurrences matches {0..*} matches {	-- Component details
				include
					archetype_id/value matches {/.*/}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Medication">
					description = <"Details of a medication or component of a medication, including details of any specific constituents.">
				>
				["at0001"] = <
					text = <"Combination pack">
					description = <"The component consists of a number of separate products which are pre-packaged by the manufacturer, for example Canesten Combi.">
				>
				["at0003"] = <
					text = <"Expiry">
					description = <"The expiry date and/or time of the medication normally as documented by the manufacturer but may be set by the individual preparing an ad hoc mixture.">
					comment = <"For example: '2017-05-23'. This is intended for the date or time as given by the manufacturer or individual preparing the mixture. Any other form of expiry date, such as time from production or depending on storage environment, can be inserted using a specific CLUSTER archetype in the Substance Details slot or added as part of the Description.
">
				>
				["at0071"] = <
					text = <"Form">
					description = <"The formulation or presentation of the medication.">
					comment = <"For example: \"tablet\", \"capsule\", \"cream\", \"infusion fluid\". Coding of the form with a terminology is preferred, where possible.">
				>
				["at0080"] = <
					text = <"Therapeutic">
					description = <"The chemical has a known and desired effect which is positive.">
				>
				["at0083"] = <
					text = <"Adjuvant">
					description = <"The chemical is inactive but aids the therapeutic effect of another ingredient.">
				>
				["at0084"] = <
					text = <"Excipient">
					description = <"The chemical is inactive and is added as a helper substance.">
				>
				["at0115"] = <
					text = <"Strength as concentration">
					description = <"The concentration of the component.">
					comment = <"For example: \"10 mg/1\", \"5 %\", \"0.125 mg/ml\", \"100 ml/l\", \"10000 SQ-U/ml\". The unit '1', for example in \"mg/1\" represents an administration unit such as tablet or puff.">
				>
				["at0126"] = <
					text = <"Constituent">
					description = <"Details of an ingredient or product used to make up a mixed pack, preparation or infusion.">
					comment = <"This is not normally required other than where an ad-hoc mixture or infusion is being described.">
				>
				["at0127"] = <
					text = <"Role">
					description = <"The role of the constituent within the mixture or infusion.">
				>
				["at0132"] = <
					text = <"Component name">
					description = <"The name of the medication component.">
					comment = <"For example: \"Zinacef 750 mg powder\" or \"cefuroxim\". This item should be coded if possible, using for example RxNorm, DM+D, Australian Medicines Terminology or FEST. Usage of component name will vary according to context of use. For example: This element may be omitted where the name of the medication is recorded in the parent INSTRUCTION or ACTION archetype, and this archetype is only used to record that the form must be or was \"liquid\".">
				>
				["at0133"] = <
					text = <"Description">
					description = <"A text description of the component where it is not possible to describe this fully using numerical strengths and amounts.">
				>
				["at0138"] = <
					text = <"Constituent details">
					description = <"Details of the constituent.">
					comment = <"This slot is intended to be used to recursively add details about the constituent using this archetype.">
				>
				["at0139"] = <
					text = <"Constituent amount">
					description = <"The amount of the constituent.">
					comment = <"For example: \"1 mg\", \"1.5 ml\", \"1000 IU\".">
				>
				["at0141"] = <
					text = <"Component details">
					description = <"Further details about the medication preparation.">
					comment = <"For example: Detailed information about the drug class or intended routes.">
				>
				["at0142"] = <
					text = <"Category">
					description = <"The nature of the medication or medication component, with regard to manufacturing and number of ingredients.">
					comment = <"For example: \"Co-codamol\" is a Combination product, while \"Morphine 60 mg + Haloperidol 2 mg + Midazolam 5 mg\" is an Ad-hoc mixture, whose composition is fully specified within the order.">
				>
				["at0143"] = <
					text = <"Ad-hoc mixture">
					description = <"The component is composed of a mixture of ingredients specified within the order. These are typically prepared by pharmacy or ward personnel to suit individual patients.">
				>
				["at0144"] = <
					text = <"Combination product">
					description = <"The component consists of a number of active ingredients which are pre-combined into a single form such as a tablet, cream or powder by the manufacturer, for example Co-codamol.">
				>
				["at0145"] = <
					text = <"Single-substance product">
					description = <"The component is a manufactured product containing a single active ingredient.">
				>
				["at0146"] = <
					text = <"Ingredient">
					description = <"The component is an individual ingredient of the medication. This term is used when the arcehtype is used recursively, to describe the individual ingredients of a medication.">
				>
				["at0148"] = <
					text = <"Alternate constituent amount">
					description = <"An alternate but equivalent representation of the amount of the constituent.">
					comment = <"For example: '5 mg' where the Constituent amount is '1 ml' as an alternate ingredient amount for a substance with strength '5mg/1ml'.">
				>
				["at0150"] = <
					text = <"Batch ID">
					description = <"The identifier assigned to the batch of medications by the manufacturer during production.">
				>
				["at0151"] = <
					text = <"Manufacturer">
					description = <"The manufacturer of the component.">
					comment = <"For example: 'Abbott'.">
				>
				["at0152"] = <
					text = <"Mixture strength">
					description = <"The strength of a mixture such as a solution or cream.">
					comment = <"Some medications are manufactured or prepared as packs of for example solutions or creams, where the entire pack is intended to be used as a single dose. The strength of such medications is often given as the amount of active ingredient in the amount of the entire pack. For example: \"2 mg/0.5 ml\", \"5 mg/10 g\" or \"10 ml/500 ml\".">
				>
				["at0153"] = <
					text = <"Substance amount">
					description = <"The amount of substance per amount of diluent.">
					comment = <"The substance amount is usually given using mass, volume or arbitrary units. For example: \"5 mg\", \"100 ml\", \"20 IU\". Where a preparation strength is described as \"5 mg/10 g\", \"5 mg\" is the substance amount.">
				>
				["at0157"] = <
					text = <"Diluent amount">
					description = <"The amount of diluent the substance is diluted in.">
					comment = <"The diluent amount is usually given using mass or volume units. For example: \"5 g\", \"100 ml\". Where a preparation strength is described as \"5 mg/10 g\", \"10 g\" is the diluent amount.">
				>
				["at0159"] = <
					text = <"Strength as mass">
					description = <"The mass of active substance per an implied dose unit of the component.">
					comment = <"Some vendors prefer giving strength for administration units such as tablets or puffs as a nominator quantity of mass, while leaving the denominator implied. The preferred way of recording strength is using the 'Strength as concentration' element.">
				>
				["at0160"] = <
					text = <"Strength">
					description = <"The strength of the component.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Legemiddel">
					description = <"Detaljer om et legemiddel eller en komponent av et legemiddel, inkludert detaljer om spesifikke bestanddeler.">
				>
				["at0001"] = <
					text = <"Kombinasjonspakning">
					description = <"Komponenten består av flere separate produkter som er forhåndspakket av produsenten, for eksempel Canesten vaginalkapsel + krem.">
				>
				["at0003"] = <
					text = <"Utløpsdato">
					description = <"Utløpsdatoen/tidspunktet for legemiddelet. Dette oppgis vanligvis av produsenten, men kan også settes av individet som klargjør en blanding.">
					comment = <"For eksempel \"2017-05-23\". Dette er ment for datoen som oppgitt av produsenten eller individet som klargjør blandingen. Andre former for utløpsdatoer som må dokumenteres, som for eksempel tid fra produksjonstidspunktet eller avhengighet av lagringsomgivelser, kan legges til som en spesifikk CLUSTER-arketype eller som en kommentar.">
				>
				["at0071"] = <
					text = <"Form">
					description = <"Formulering eller form av legemiddelet.">
					comment = <"For eksempel \"tablett\", \"kapsel\", \"krem\", infusjonsvæske\". Koding av legemiddelformen med en terminologi foretrekkes der det er mulig.">
				>
				["at0080"] = <
					text = <"Terapeutisk">
					description = <"Ingrediensen har en kjent og ønsket positiv effekt.">
				>
				["at0083"] = <
					text = <"Adjuvant">
					description = <"Ingrediensen modifiserer og forsterker effekten av andre substanser, men har liten eller ingen virkning i seg selv.">
				>
				["at0084"] = <
					text = <"Hjelpestoff">
					description = <"Ingrediensen er inaktiv, og fungerer som hjelpesubstans.">
				>
				["at0115"] = <
					text = <"Styrke som konsentasjon">
					description = <"Konsentrasjonen av komponenten.">
					comment = <"For eksempel \"10mg/1\", \"5 %\", \"0,125 mg/ml\", \"100 ml/l\", \"10000 SQ-U/ml\". Enheten '1', for eksempel i \"mg/1\", representerer en administrasjonsenhet som for eksempel tablett eller inhalasjon.">
				>
				["at0126"] = <
					text = <"Ingrediens">
					description = <"Detaljer om ingrediensen som brukes for å lage et blandingspreparat eller en infusjon.">
					comment = <"Dette kreves vanligvis kun når en magistrellforskrevet blanding eller infusjon beskrives.">
				>
				["at0127"] = <
					text = <"Rolle">
					description = <"Ingrediensens rolle i en blanding eller infusjon.">
				>
				["at0132"] = <
					text = <"Komponentnavn">
					description = <"Navn på legemiddelkomponenten.">
					comment = <"For eksempel \"Zinacef 750 mg pulver til infusjonsvæske\" eller \"cerfuroxim\". Dette elementet bør kodes om mulig, for eksempel med FEST. Bruk av Komponentnavn-elementet vil variere med brukssammenhengen. For eksempel vil elementet ikke brukes dersom legemiddelnavnet er registrert i den overordnede INSTRUCTION- eller ACTION-arketypen, og denne arketypen kun brukes for å registrere at legemiddelformen må være eller var \"væske\".">
				>
				["at0133"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse av komponenten der det ikke er mulig med en fullstendig beskrivelse ved hjelp av styrke eller mengde beskrevet med tall.">
				>
				["at0138"] = <
					text = <"Ingrediensdetaljer">
					description = <"Detaljer om ingrediensen.">
					comment = <"Dette SLOTet er ment å brukes for å rekursivt registrere detaljer om ingrediensen ved hjelp av denne arketypen.">
				>
				["at0139"] = <
					text = <"Mengde ingrediens">
					description = <"Mengden ingrediens.">
					comment = <"For eksempel \"1 mg\", \"1.5 ml\", \"1000 IE\".">
				>
				["at0141"] = <
					text = <"Komponentdetaljer">
					description = <"Ytterligere detaljer om legemiddelkomponenten.">
					comment = <"For eksempel detaljert informasjon om legemiddelklasse eller tiltenkte administrasjonsveier.">
				>
				["at0142"] = <
					text = <"Kategori">
					description = <"Legemiddelkomponentens kategori med tanke på tilvirkning og antall ingredienser.">
					comment = <"For eksempel er Paralgin Forte et kombinasjonsprodukt, mens \"Morfin 60 mg + Haloperidol 2 mg + Midazolam 5 mg\" er en magistrellforskrevet blanding, som er fullt spesifisert i ordineringen.">
				>
				["at0143"] = <
					text = <"Magistrellforskrevet blanding">
					description = <"Legemiddelet er en sammensatt blanding av ingredienser som er spesifisert i en ordinering Disse tillages typisk av apotek- eller sengepostpersonell for individuelle pasienter.">
				>
				["at0144"] = <
					text = <"Kombinasjonsprodukt">
					description = <"Komponenten består av flere aktive ingredienser som er satt sammen til én form som en tablett, krem eller pulver av produsenten. For eksempel Paralgin Forte.">
				>
				["at0145"] = <
					text = <"Produkt som består av én legemiddelsubsans">
					description = <"Komponenten er et fabrikkframstilt produkt som kun har én aktiv ingrediens.">
				>
				["at0146"] = <
					text = <"Bestanddel">
					description = <"Komponenten er en individuell ingrediens i legemiddelet. Denne termen benyttes når arketypen brukes rekursivt, for å beskrive de individuelle ingrediensene i et legemiddel.">
				>
				["at0148"] = <
					text = <"Alternativ ingrediensmengde">
					description = <"En alternativ men tilsvarende representasjon av mengden ingrediens.">
					comment = <"For eksempel \"5 mg\" der ingrediensmengden er \"1 ml\", som en alternativ mengdeangivelse av en ingrediens som har styrke \"5 mg/ml\".">
				>
				["at0150"] = <
					text = <"Batch-ID">
					description = <"Identifikatoren tilordnet av produsenten for å identifisere produksjons-batchen for produktet.">
				>
				["at0151"] = <
					text = <"Produsent">
					description = <"Produsenten av komponenten.">
					comment = <"For eksempel \"Abbott\".">
				>
				["at0152"] = <
					text = <"Strukturert styrke">
					description = <"Strukturert, kompleks styrke av komponenten.">
					comment = <"Noen legemidler produseres eller tillages som pakninger av for eksempel oppløsninger eller kremer, der hele pakken er tiltenkt brukt som en enkelt dose. Styrken av slike legemidler oppgis ofte som mengden aktiv ingrediens per mengden produkt i hele pakningen. For eksempel \"2 mg/0,5 ml\", \"5 mg/10 g\" eller \"10 ml/500 ml\".">
				>
				["at0153"] = <
					text = <"Mengde substans">
					description = <"Mengden substans per mengde fortynningsmiddel.">
					comment = <"Substansmengden oppgis som regel ved hjelp av enheter for masse, volum eller arbitrære enheter. For eksempel \"5 mg\", \"100 ml\" eller \"20 IE\". Der en styrke er oppgitt som \"5 mg/10 g\", er \"5 mg\" mengden substans.">
				>
				["at0157"] = <
					text = <"Mengde fortynningsmiddel">
					description = <"Mengden fortynningsmiddel substansen er fortynnet i.">
					comment = <"Mengden fortynningsmoddel oppgis som regel i enheter for masse eller volum. For eksempel \"5 g\" eller \"100 ml\". Der styrke er oppgitt som \"5 mg/10 g\", er \"10 g\" mengden fortynningsmiddel.">
				>
				["at0159"] = <
					text = <"Styrke som masse">
					description = <"Massen av den aktive substansen, per en implisitt doseenhet av komponenten.">
					comment = <"Noen leverandører foretrekker å oppgi styrke for administrasjonsenheter som tabletter eller inhalasjoner som kun massen av telleren i konsenstrasjonsbrøken, og la nevneren være implisitt. Den foretrukne måten å registrere styrke på er ved å bruke elementet \"Styrke som konsentasjon\".">
				>
				["at0160"] = <
					text = <"Styrke">
					description = <"Styrken av komponenten.">
				>
			>
		>
		["pt"] = <
			items = <
				["at0000"] = <
					text = <"*Medication(en)">
					description = <"*Details of a medication or component of a medication, including details of any specific constituents.(en)">
				>
				["at0001"] = <
					text = <"*Combination pack(en)">
					description = <"*The component consists of a number of separate products which are pre-packaged by the manufacturer.(en)">
				>
				["at0003"] = <
					text = <"*Expiry(en)">
					description = <"*The expiry date and/or time of the medication normally  as documented by the manufacturer but may be set by the individual preparing an ad hoc mixture.(en)">
					comment = <"*For example: '2017-05-23'. This is intended for the date or time as given by the manufacturer or individual preparing the mixture. Any other form of expiry date, such as time from production or depending on storage environment, can be inserted using a specific CLUSTER archetype in the Substance Details slot or added as part of the Description.
(en)">
				>
				["at0071"] = <
					text = <"*Form(en)">
					description = <"*The formulation or presentation of the medication.(en)">
					comment = <"*For example: \"tablet\", \"capsule\", \"cream\", \"infusion fluid\".(en)">
				>
				["at0080"] = <
					text = <"Terapêutico">
					description = <"Este elemento químico tem um efeito conhecido e considerado como positivo.">
				>
				["at0083"] = <
					text = <"Coadjuvante">
					description = <"Este elemento químico é ativo mas apoia o efeito terapêutico de outro ingrediente.">
				>
				["at0084"] = <
					text = <"Diluente">
					description = <"Diluente inerte.">
				>
				["at0115"] = <
					text = <"*Strength as concentration(en)">
					description = <"*The concentration of the component.(en)">
					comment = <"*For example: \"10 mg/1\", \"5 %\", \"0.125 mg/ml\", \"100 ml/l\", \"10000 [arb'u]{SQ-U}/ml\". The unit '1', for example in \"mg/1\" represent the amount of substance per administration unit such as tablet or puff.(en)">
				>
				["at0126"] = <
					text = <"Ingrediente">
					description = <"Detalhes de um ingrediente utilizado para fazer uma preparação manipulada ou infusão.">
					comment = <"Este ítem só é exigido quando se tratar de um preparação manipulada ou de infusões.">
				>
				["at0127"] = <
					text = <"*Role(en)">
					description = <"*The role of the constituent within the mixture or infusion.(en)">
				>
				["at0132"] = <
					text = <"*Component name(en)">
					description = <"*The name of the medication component.(en)">
					comment = <"*For example: \"Zinacef 750 mg powder\" or \"cefuroxim\". This item should be coded if possible. Usage of component name will vary according to context of use.(en)">
				>
				["at0133"] = <
					text = <"*Description(en)">
					description = <"*A text description of the component  where it is not possible to describe this fully using numerical strengths and amounts.(en)">
				>
				["at0138"] = <
					text = <"*Constituent details(en)">
					description = <"*Details of the constituent.(en)">
				>
				["at0139"] = <
					text = <"*Constituent amount(en)">
					description = <"*The amount of the constituent.(en)">
					comment = <"*For example: \"1 mg\", \"1.5 ml\", \"1000 IU\".(en)">
				>
				["at0141"] = <
					text = <"*Component details(en)">
					description = <"*Further details about the medication preparation.(en)">
					comment = <"*For example: Detailed information about the drug class or intended routes.(en)">
				>
				["at0142"] = <
					text = <"*Category(en)">
					description = <"*The nature of the medication component, with regard to manufacturing and number of ingredients.(en)">
					comment = <"*For example: Co-codamol is a Combination product, wile 50% white soft paraffin + 50% white soft paraffin is an Ad-hoc mixture, whose composition is fully specified within the order. (en)">
				>
				["at0143"] = <
					text = <"*Ad-hoc mixture(en)">
					description = <"*The component is composed of a mixture of ingredients specified within the order.(en)">
				>
				["at0144"] = <
					text = <"*Combination product(en)">
					description = <"*The component consists of a number of ingredients which are pre-combined by the manufacturer.(en)">
				>
				["at0145"] = <
					text = <"*Single-substance product(en)">
					description = <"*The component is a manufactured product containing a single ingredient.(en)">
				>
				["at0146"] = <
					text = <"*Ingredient(en)">
					description = <"*The component is an individual ingredient of the medication.(en)">
				>
				["at0148"] = <
					text = <"*Alternate constituent amount(en)">
					description = <"*An alternate but equivalent representation of the amount of the constituent.(en)">
					comment = <"*For example: '5 mg' where the Constituent amount is '1 ml' as an alternate ingredient amount for a substance with strength '5mg/1ml'.(en)">
				>
				["at0150"] = <
					text = <"*Batch ID(en)">
					description = <"*The identifier assigned to the batch of medications by the manufacturer during production.(en)">
				>
				["at0151"] = <
					text = <"*Manufacturer(en)">
					description = <"*The manufacturer of the component.(en)">
					comment = <"*For example: 'Abbott'.(en)">
				>
				["at0152"] = <
					text = <"*Structured strength(en)">
					description = <"*Structured complex strength of the component.(en)">
					comment = <"*For example: \"2 mg/0.5 ml\", \"5 mg/10 g\" or \"10 ml/500 ml\".(en)">
				>
				["at0153"] = <
					text = <"*Substance amount(en)">
					description = <"*The amount of substance per amount of diluent.(en)">
					comment = <"*The substance amount is usually given using mass, volume or arbitrary units. However, there may be examples where other unit dimensions are used. For example: \"5 mg\", \"100 ml\", \"20 IU\"
. Where a preparation strength is described as \"5 mg/10 g\", \"5 mg\" is the substance amount.(en)">
				>
				["at0157"] = <
					text = <"*Diluent amount(en)">
					description = <"*The amount of diluent the substance is diluted in.(en)">
					comment = <"*The diluent amount is usually given using mass or volume units. However, there may be examples where other unit dimensions are used. For example: \"5 g\", \"100 mg\"
. Where a preparation strength is described as \"5 mg/10 g\", \"10 g\" is the diluent amount.(en)">
				>
				["at0159"] = <
					text = <"*Strength as mass(en)">
					description = <"*The mass of active substance per an implied dose unit of the component.(en)">
					comment = <"*Some vendors prefer giving strength for administration units such as tablets or puffs as a nominator quantity of mass, while leaving the denominator implied. The preferred way of recording strength is using the 'Strength as concentration' element.(en)">
				>
				["at0160"] = <
					text = <"*Strength(en)">
					description = <"*The strength of the component.(en)">
				>
			>
		>
	>
