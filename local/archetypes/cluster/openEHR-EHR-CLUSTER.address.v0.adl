archetype (adl_version=1.4; uid=2790f093-d7dc-4ee8-8236-881f853ec9b7)
	openEHR-EHR-CLUSTER.address.v0

concept
	[at0000]	-- Address
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2021-04-14">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about a physical or postal address for use within a health record.">
			use = <"Use to record details about a physical or postal address required as part of clinical recording within a health record. Examples include:
- the address of an attending home care organisation in a referral to another healthcare provider; or
- the address of a person or organisation that holds a physical copy of an advance care directive; or
- the address of a location of a fall or accident.

This CLUSTER.address could also be used as a proxy for formal demographic address data when reviewing a template with clinicians - for example, an assessment where reviewers would expect to see the individual's full contact details at the top of the assessment form.

This archetype has been designed so that one or more 'Address line' elements can be used to record a simple address as lines of free text. 

Each 'Address line' element can be renamed in a template using the run-time name constraints eg 'Address line 1', 'Address line 2' etc.

If the CLUSTER.structured_address is nested within the 'Structured address' SLOT, any or all of the structured address elements can be concatenated and represented in one or more 'Address line' elements, as required.">
			keywords = <"address", ...>
			misuse = <"Not to be used to record an address for the purposes of maintaining a demographic register or index for an individual or an organisation. Use demographic archetypes or a formal Master patient index or Healthcare provider index for this purpose.">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"6E444F2637B40C74548CD3E75E155DAC">
		["build_uid"] = <"dacb6a37-a245-4ae0-9a57-57f36422fc27">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Address
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {    -- Type
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0002,    -- Physical
							at0003,    -- Postal
							at0004]    -- Both
						}
					}
				}
			}
			ELEMENT[at0005] occurrences matches {0..1} matches {    -- Use
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0006,    -- Business
							at0007,    -- Residential
							at0008]    -- Temporary accomodation
						}
					}
				}
			}
			ELEMENT[at0009] occurrences matches {0..*} matches {    -- Address line
				value matches {
					DV_TEXT matches {*}
				}
				name matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0010,    -- Address line 1
							at0011,    -- Address line 2
							at0012,    -- Address line 3
							at0013]    -- Address line 4
						}
					}
				}
			}
			allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Structured address line
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.structured_address\.v0/}
			}
			ELEMENT[at0015] occurrences matches {0..*} matches {    -- Suburb/Town/City/Locality
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0016] occurrences matches {0..*} matches {    -- District/County
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0017] occurrences matches {0..1} matches {    -- State/Territory/Province
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0018] occurrences matches {0..1} matches {    -- Postcode
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0019] occurrences matches {0..1} matches {    -- Country
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0020] occurrences matches {0..1} matches {    -- Postal delivery point
				value matches {
					DV_IDENTIFIER matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Address">
					description = <"A physical or postal address for use within a health record.">
				>
				["at0009"] = <
					text = <"Address line">
					description = <"A text composite of one or more structured address components that represent a low level of geographical/physical description of a location that, used in conjunction with the other high-level  address components i.e. ‘Suburb/Town/Locality’, ‘Postcode’, and ‘State/Territory/Province’, forms a complete geographical/physical address.">
					comment = <"Run-time name constraints allow for multiple named 'Address lines', as required. For example: 
Apartment 7A,
52 Davis Street,
Carlton North,
Victoria, AUSTRALIA 3042.">
				>
				["at0014"] = <
					text = <"Structured address line">
					description = <"Additional structured low level geographical/physical details about a location that can be concatenated to form one or more 'Address line' entries.">
				>
				["at0015"] = <
					text = <"Suburb/Town/City/Locality">
					description = <"The name of the general locality containing the address.">
					comment = <"Coding with an external terminology is preferred, where possible. For example: Fitzroy, Calgary.">
				>
				["at0016"] = <
					text = <"District/County">
					description = <"The name of an internal political or geographic district or area within a state, territory or province containing the address.">
					comment = <"Coding with an external terminology is preferred, where possible. ">
				>
				["at0017"] = <
					text = <"State/Territory/Province">
					description = <"The name of an internal political or geographic division of a country containing the address.">
					comment = <"Coding with an external terminology is preferred, where possible. For example: Victoria; Alberta.">
				>
				["at0018"] = <
					text = <"Postcode">
					description = <"The code for a postal delivery area containing the address, aligned with locality, suburb or place for an address, as defined by the relevant postal delivery service.">
					comment = <"Also known as ZIP code.
">
				>
				["at0019"] = <
					text = <"Country">
					description = <"The name of the country containing the address.">
					comment = <"Coding with an external terminology is preferred, where possible. For example: Australia; Canada">
				>
				["at0020"] = <
					text = <"Postal delivery point">
					description = <"A unique number assigned to a postal delivery point, as defined by the relevant postal delivery service.">
				>
				["at0010"] = <
					text = <"Address line 1">
					description = <"">
				>
				["at0011"] = <
					text = <"Address line 2">
					description = <"">
				>
				["at0012"] = <
					text = <"Address line 3">
					description = <"">
				>
				["at0013"] = <
					text = <"Address line 4">
					description = <"">
				>
				["at0005"] = <
					text = <"Use">
					description = <"The primary purpose or use for the address.">
				>
				["at0006"] = <
					text = <"Business">
					description = <"Address of the physical location of a business or office.">
				>
				["at0007"] = <
					text = <"Residential">
					description = <"Address of where a person is living on a regular basis.">
				>
				["at0008"] = <
					text = <"Temporary accomodation">
					description = <"Address of where a person is living on a temporary basis.">
				>
				["at0001"] = <
					text = <"Type">
					description = <"The type of address.">
					comment = <"For example: physical or postal.">
				>
				["at0002"] = <
					text = <"Physical">
					description = <"Address of the physical location.">
				>
				["at0003"] = <
					text = <"Postal">
					description = <"Address for sending correspondence.">
				>
				["at0004"] = <
					text = <"Both">
					description = <"Address of a physical location and for sending correspondence.">
				>
			>
		>
	>
