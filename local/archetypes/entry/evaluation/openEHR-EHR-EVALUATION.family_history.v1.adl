archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.family_history.v1

concept
	[at0000]	-- Family History
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2010-12-15">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record information about the occurrence of health problems or diagnoses in any family members - live or deceased. The scope of this archetype is also open to information about non-genetic family members where health may affected from social or environmental factors.">
			use = <"Use to record a summary of information about problems or diagnoses found in family members. This information may be used as the basis for assessment of risk to the individual from potentially inheritable conditions or initiation of preventive health activities.

Additional detail about a family member's specific problem or diagnosis can be captured using the EVALUATION.problem_diagnosis archetype and specifying the 'Subject of Care' as the relative.

This archetype can be used as the basis for a Family Pedigree chart of health problems/diagnoses or to support estimations of risk of a condition based on prevalence in the family history.

It may be necessary to identify each family member specifically and not just by the relationship to the patient. For example, while there will be only one maternal grandmother, there may be many female maternal cousins. This will ensure that a pedigree chart is accurate and allow for additional problems/diagnoses to be added, or existing records to be updated, for each identified family member. If the record is private and will not be shared, for reasons of clarity it may be preferable to record the relative's name. If the record is to be shared, the family member should be identified by a unique label or alias.">
			keywords = <"family", "history", "health", "condition", "problem", "diagnosis", "genetic", "pedigree">
			misuse = <"Not to be used to record information about the relative or calculated risk of developing a condition due to family history - use the EVALUATION.risk family of archetypes.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Rita Apelt, Department of Health,NT, Australia", "John Bennett, NEHTA, Australia", "Stephen Chu, NEHTA, Australia", "David Evans, Queensland Health, Australia", "Tim Garden, NTG Department of Health, Australia", "Jacquie Garton-Smith, Royal Perth Hospital and DoHWA, Australia", "Andrew Goodchild, NEHTA, Australia", "Sam Heard, Ocean Informatics, Australia (Editor)", "Mary Kelaher, NEHTA, Australia", "Robert L'egan, NEHTA, Australia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Hugh Leslie, Ocean Informatics, Australia", "Mike Martyn, The Hobart Anaesthetic Group, Australia", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "Chris Mitchell, RACGP, Australia", "Stewart Morrison, NEHTA, Australia", "Jeremy Oats, NT Health, Australia", "Lynne Parsons, Primary and Community Health Services, Australia", "Jodie Pycroft, Adelaide Northern Division of General Practice Ltd, Australia", "Robyn Richards, NEHTA - Clinical Terminology, Australia", "Elizabeth Stanick, Hobart Anaesthetic Group, Australia", "John Taylor, NEHTA, Australia", "Gordon Tomes, Australian Institute of Health and Welfare, Australia", "Richard Townley-O'Neill, NEHTA, Australia", "Donna Truran, ACCTI-UoW, Australia", "Jo Wright, NT Dept of Health, Australia (Editor)">
	other_details = <
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["references"] = <"Family History, draft archetype, NEHTA Clinical Knowledge Manager [Internet]. Australia, National eHealth Transition Authority. Authored: 15 Dec 2010. Available at: 
http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.927 (last accessed 05 Mar 2015).

Risk of condition based on family history, rejected archetype, openEHR Clinical Knowledge Manager [Internet]. openEHR Foundation. Authored: 23 Apr 2006. Available at: http://www.openehr.org/ckm/#showArchetype_1013.1.125 (last accessed 05 Mar 2015).

HL7 Version 3 Standard: Clinical Genomics; Pedigree, Release 1. ANSI/HL7 V3 CGPED, R1-2007. Published 2007 May 07. Available at: http://www.hl7.org/implement/standards/product_brief.cfm?product_id=8 (last accessed 05 Mar 2015).">
		["MD5-CAM-1.0.1"] = <"2E342788B82AFE6E24E8BEC5B674ADC0">
	>

definition
	EVALUATION[at0000] matches {	-- Family History
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Summary
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0028] occurrences matches {0..*} matches {	-- Per Condition
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0029] occurrences matches {0..1} matches {	-- Index condition name
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0036] occurrences matches {0..1} matches {	-- Present?
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							ELEMENT[at0030] occurrences matches {0..1} matches {	-- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
							CLUSTER[at0031] occurrences matches {0..*} matches {	-- Affected family
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0032] occurrences matches {0..1} matches {	-- Relationship
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0037, 	-- First degree relative
													at0038, 	-- Second degree relative
													at0039, 	-- Third degree relative
													at0040, 	-- Maternal relative
													at0041, 	-- Paternal relative
													at0042]	-- Family
												}
											}
										}
									}
									ELEMENT[at0033] occurrences matches {0..1} matches {	-- Sex
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0043, 	-- Male
													at0044]	-- Female
												}
											}
										}
									}
									ELEMENT[at0034] occurrences matches {0..1} matches {	-- Number affected
										value matches {
											DV_COUNT matches {*}
										}
									}
									ELEMENT[at0035] occurrences matches {0..1} matches {	-- Number unaffected
										value matches {
											DV_COUNT matches {*}
										}
									}
								}
							}
						}
					}
					CLUSTER[at0003] occurrences matches {0..*} matches {	-- Per family member
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0004] occurrences matches {0..1} matches {	-- Name
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0020] occurrences matches {0..1} matches {	-- Alias
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0016] occurrences matches {0..1} matches {	-- Relationship
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0005] occurrences matches {0..1} matches {	-- Date of birth
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0023] occurrences matches {0..1} matches {	-- Deceased?
								value matches {
									DV_BOOLEAN matches {
										value matches {True}
									}
								}
							}
							ELEMENT[at0011] occurrences matches {0..1} matches {	-- Age at death
								value matches {
									DV_DURATION matches {*}
								}
							}
							CLUSTER[at0008] occurrences matches {0..*} matches {	-- Medical History
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Problem/Diagnosis
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0012] occurrences matches {0..1} matches {	-- Clinical Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0010] occurrences matches {0..1} matches {	-- Age at Onset
										value matches {
											DV_DURATION matches {*}
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {	-- Cause of Death?
										value matches {
											DV_BOOLEAN matches {
												value matches {True, False}
											}
										}
									}
								}
							}
							CLUSTER[at0024] occurrences matches {0..1} matches {	-- Genetic Markers
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0022] occurrences matches {0..1} matches {	-- Marker Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									allow_archetype ITEM[at0027] occurrences matches {0..*} matches {	-- Genetic Marker Details
										include
											archetype_id/value matches {/.*/}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0025] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0026] occurrences matches {0..1} matches {	-- Last Updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0045] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Family History">
					description = <"Summary information about the problems and/or diagnoses found in family members.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Summary">
					description = <"Narrative overview about problems, diagnoses, and genetic markers present in family members.">
					comment = <"Use this field to capture unstructured family history information recorded in clinical records or to import textual data from existing/legacy clinical systems.">
				>
				["at0003"] = <
					text = <"Per family member">
					description = <"Details about a specific family member.">
				>
				["at0004"] = <
					text = <"Name">
					description = <"Name of family member.">
					comment = <"For privacy reasons this may not be appropriate for recording, sharing or public display and in this situation the 'Alias' should be used.">
				>
				["at0005"] = <
					text = <"Date of birth">
					description = <"Date of birth of the family member.">
					comment = <"At least a partial date will be useful to calculate an age. For example, current age of relative in a pedigree chart.">
				>
				["at0008"] = <
					text = <"Medical History">
					description = <"Detail about problems or diagnoses for the family member.">
				>
				["at0009"] = <
					text = <"Problem/Diagnosis">
					description = <"Identification of the family member's problem or diagnosis.">
					comment = <"Coding of the problem or diagnosis with a terminology is preferred, where possible. May link from this data element to a detailed record of a Problem/Diagnosis using the EVALUATION.problem_diagnosis archetype with the Subject of Care set to the family member, not to the patient.">
				>
				["at0010"] = <
					text = <"Age at Onset">
					description = <"Estimated or actual age of the family member when the problem/diagnosis was clinically recognised.">
				>
				["at0011"] = <
					text = <"Age at death">
					description = <"Age of the family member at death.">
				>
				["at0012"] = <
					text = <"Clinical Description">
					description = <"Narrative description or comments about clinical aspects of the family member's problem/diagnosis.">
				>
				["at0014"] = <
					text = <"Cause of Death?">
					description = <"True if the problem/diagnosis was a direct cause of death in this family member.">
				>
				["at0016"] = <
					text = <"Relationship">
					description = <"The relationship of the family member to the subject of care.">
					comment = <"Coding of the relationship with a terminology is preferred, where possible and including specification of maternal and paternal as required.">
				>
				["at0020"] = <
					text = <"Alias">
					description = <"An alternative name or label to uniquely identify a family member, without using a personal name which might publicly identify the individual.">
					comment = <"To be used to assist in distinguishing one individual from multiple family members with identical relationships. For example, the label to distinguish one specific sister from three known sisters might be 'eldest sister' or 'sister #1'.">
				>
				["at0022"] = <
					text = <"Marker Description">
					description = <"Description of risk-related genetic markers identified in this family member.">
				>
				["at0023"] = <
					text = <"Deceased?">
					description = <"Is the family member is deceased?">
					comment = <"Record as 'True' if family member is deceased.">
				>
				["at0024"] = <
					text = <"Genetic Markers">
					description = <"Detailed genetic marker information for the family member.">
					comment = <"Note: More data elements will be needed in future to record detailed genetic marker information.">
				>
				["at0025"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0026"] = <
					text = <"Last Updated">
					description = <"The date this Family History Summary was last updated.">
				>
				["at0027"] = <
					text = <"Genetic Marker Details">
					description = <"Details about the relative's genetic markers.">
				>
				["at0028"] = <
					text = <"Per Condition">
					description = <"Details about the conditions that occur in the subject's family.">
				>
				["at0029"] = <
					text = <"Index condition name">
					description = <"Name of the disease, illness, condition or problem.">
					comment = <"Coding of the index condition with a terminology is preferred, where possible.">
				>
				["at0030"] = <
					text = <"Description">
					description = <"Narrative description about the index condition's occurrence in family members.">
				>
				["at0031"] = <
					text = <"Affected family">
					description = <"The number of family members affected by the index condition.">
				>
				["at0032"] = <
					text = <"Relationship">
					description = <"The proximity or line of the family member.">
				>
				["at0033"] = <
					text = <"Sex">
					description = <"Biological sex of the relative/s.">
				>
				["at0034"] = <
					text = <"Number affected">
					description = <"The number of family members affected by the index condition.">
				>
				["at0035"] = <
					text = <"Number unaffected">
					description = <"The number of family members unaffected by the index condition.">
				>
				["at0036"] = <
					text = <"Present?">
					description = <"There is a positive family history of the index condition.">
					comment = <"Record as 'True' if the index condition has been identified in genetic family members.">
				>
				["at0037"] = <
					text = <"First degree relative">
					description = <"50% genetic share with the subject - parent, sibling or child.">
				>
				["at0038"] = <
					text = <"Second degree relative">
					description = <"25% genetic share with the subject - grandparent, aunt, uncle, niece, nephew and grandchildren.">
				>
				["at0039"] = <
					text = <"Third degree relative">
					description = <"12.5% genetic share with the subject - great grandparent, great aunt, great uncle, first cousin, children of nieces and nephews, and great grandchildren.">
				>
				["at0040"] = <
					text = <"Maternal relative">
					description = <"Related through the subject's mother.">
				>
				["at0041"] = <
					text = <"Paternal relative">
					description = <"Related through the subject's father.">
				>
				["at0042"] = <
					text = <"Family">
					description = <"Entire genetic family of the subject.">
				>
				["at0043"] = <
					text = <"Male">
					description = <"family member who is biologically male.">
				>
				["at0044"] = <
					text = <"Female">
					description = <"family member who is biologically female.">
				>
				["at0045"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: e.g. Local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
			>
		>
	>
