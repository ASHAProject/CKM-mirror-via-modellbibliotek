archetype (adl_version=1.4; uid=035d8e23-ab6f-40d7-9a46-3c9def35de43)
	openEHR-EHR-EVALUATION.biospecimen_summary-semen.v0
specialise
	openEHR-EHR-EVALUATION.biospecimen_summary.v0

concept
	[at0000.1]	-- Semen summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2022-02-09">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a summary or persistent record about a sperm specimen.">
			use = <"Use to record summary or persistent record about about a sperm specimen.

This archetype has been designed to track the collection, storage, management, use or disposition of a sperm specimen, especially in assisted reproduction treatment.

Details about the biospecimen, including collection information, can be recorded using the CLUSTER.specimen in the 'Specimen details' SLOT.

Details about the location of the biospecimen can be recorded using the CLUSTER.specimen_container and CLUSTER.biobank in the 'Storage details' SLOT.">
			keywords = <"assisted reproduction", "testicular", "testicle", "tissue", "semen", "sperm">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Peter Fedorcsak, University of Oslo, Norway (openEHR Editor)", "Liv Laugen, ​Oslo University Hospital, Norway, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Ashford, P., et al., Standard terminology for reproductive tissue and cell products for use in ART. Human Reproduction Open, 2019. 2019(2): p. hoz005.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D1FA9AFA1E9466CEBD39D506A0105111">
		["build_uid"] = <"698afd42-4656-4356-86d7-6aa6f3496b27">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000.1] matches {    -- Semen summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..*} matches {    -- Biospecimen ID
						value matches {
							DV_IDENTIFIER matches {*}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003.1] matches {    -- Biospecimen type
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.1,    -- Semen
									at0.2]    -- Testicular tissue
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {    -- Treatment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {    -- Date of preservation
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {    -- Type of preservation
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0008] occurrences matches {0..*} matches {    -- Specimen details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.specimen(-[a-zA-Z0-9_]+)*\.v2|openEHR-EHR-CLUSTER\.specimen(-[a-zA-Z0-9_]+)*\.v1/}
					}
					allow_archetype CLUSTER[at0009] occurrences matches {0..*} matches {    -- Storage details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.biobank_storage(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.biobank_storage(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.specimen_container(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.specimen_container(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0010] occurrences matches {0..1} matches {    -- Disposition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {    -- Date of disposition
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0012] occurrences matches {0..1} matches {    -- Expiry date
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0016] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0.3] occurrences matches {0..1} matches {    -- Sperm procurement
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.4,    -- Ejaculation
									at0.5,    -- Epididymal aspirate
									at0.6,    -- Testicular aspirate
									at0.7,    -- Testicular biopsy
									at0.8]    -- Unknown
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0.9] occurrences matches {0..1} matches {    -- Sperm preparation
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.10,    -- Unwashed
									at0.11,    -- Washed
									at0.12,    -- Prepared
									at0.13]    -- Unknown
								}
							}
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0013] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0014] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0015] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000.1"] = <
					text = <"Semen summary">
					description = <"Summary or persistent information about a sperm specimen.">
				>
				["at0.3"] = <
					text = <"Sperm procurement">
					description = <"The method used to collect or recover the sperm.">
				>
				["at0.4"] = <
					text = <"Ejaculation">
					description = <"Sperm procured from ejaculate.">
				>
				["at0.5"] = <
					text = <"Epididymal aspirate">
					description = <"Sperm procured by aspiration from epididymis.">
				>
				["at0.6"] = <
					text = <"Testicular aspirate">
					description = <"Sperm procured by percutaneous aspiration from testis.">
				>
				["at0003.1"] = <
					text = <"Biospecimen type">
					description = <"The type of cell or tissue collected.">
				>
				["at0.1"] = <
					text = <"Semen">
					description = <"The male gamete.">
				>
				["at0.2"] = <
					text = <"Testicular tissue">
					description = <"Fragment of the testicle.">
				>
				["at0.7"] = <
					text = <"Testicular biopsy">
					description = <"Sperm procured by biopsy of testis.">
				>
				["at0.8"] = <
					text = <"Unknown">
					description = <"Procurement method is not specified, or not applicable.">
				>
				["at0.9"] = <
					text = <"Sperm preparation">
					description = <"The treatment of the sperm.">
				>
				["at0.10"] = <
					text = <"Unwashed">
					description = <"Raw ejaculate.">
				>
				["at0.11"] = <
					text = <"Washed">
					description = <"The ejaculate has been washed by centrifugation in a buffer solution.">
				>
				["at0.12"] = <
					text = <"Prepared">
					description = <"Viable sperm cells have been isolated from other contents of the seminal fluid.">
				>
				["at0.13"] = <
					text = <"Unknown">
					description = <"No information about the preparation of sperm is provided, or not applicable.">
				>
				["at0000"] = <
					text = <"Biospecimen summary">
					description = <"Summary or persistent information about one or more cells or a tissue sample collected or harvested.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Biospecimen ID">
					description = <"Unique identifier for the biospecimen.">
					comment = <"For example: SEC ID in Europe for assisted reproduction treatments.">
				>
				["at0003"] = <
					text = <"Biospecimen type">
					description = <"The type of cell or tissue collected.">
				>
				["at0007"] = <
					text = <"Type of preservation">
					description = <"The technique used to preserve the biospecimen.">
				>
				["at0006"] = <
					text = <"Date of preservation">
					description = <"The date/time when the biospecimen was preserved.">
				>
				["at0010"] = <
					text = <"Disposition">
					description = <"The final disposition of the biospecimen.">
					comment = <"Usually this involves use, donation or disposal.">
				>
				["at0011"] = <
					text = <"Date of disposition">
					description = <"The date/time when the biospecimen was preserved.">
				>
				["at0012"] = <
					text = <"Expiry date">
					description = <"The date beyond the biospecimen is not to be used.">
				>
				["at0013"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Last updated">
					description = <"The date this biospecimen summary was last updated.">
				>
				["at0015"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0004"] = <
					text = <"Description">
					description = <"Narrative description about the biospecimen.">
					comment = <"For example, information about how it was collected.">
				>
				["at0009"] = <
					text = <"Storage details">
					description = <"Details about the storage of the biospecimen.">
					comment = <"For example: details about the container and biobank where the biospecimen is stored.">
				>
				["at0005"] = <
					text = <"Treatment">
					description = <"Narrative description about the management of the biospecimen prior to preservation, including clinical treatments.">
					comment = <"It is possible to link from this data element to an ACTION.procedure within the health record to record the specific details about the clinical procedure/s and timing.">
				>
				["at0008"] = <
					text = <"Specimen details">
					description = <"Details about the biospecimen.">
					comment = <"For example: date & time of collection.">
				>
				["at0016"] = <
					text = <"Comment">
					description = <"Additional narrative about the biospecimen, not captured in other fields.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0.1"] = <[SNOMED-CT::734846002]>
			>
		>
	>
