archetype (adl_version=1.4; uid=b028aad6-d916-470a-b941-f39d63e2908f)
	openEHR-EHR-EVALUATION.biospecimen_summary-oocyte.v0
specialise
	openEHR-EHR-EVALUATION.biospecimen_summary.v0

concept
	[at0000.1]	-- Oocyte summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2022-01-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record a summary or persistent record about a single biospecimen or biosample.">
			use = <"Use to record summary or persistent record about a single biospecimen or biosample, from the time of collection or harvesting to disposition.

This archetype is intended to support tracking the collection, storage, use or disposition of human cells or tissue. Use cases include:
- oocytes, zygotes, embryos, or ejaculate in assisted reproduction; and
- autologous skin graft.">
			keywords = <"assisted reproduction", "sperm", "testicular", "ovarian", "tissue", "oocyte", "zygote", "embryo", "blastocyst">
			misuse = <"Not to be used to record blood donation. Use purpose-specific archetypes for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Ashford, P., et al., Standard terminology for reproductive tissue and cell products for use in ART. Human Reproduction Open, 2019. 2019(2): p. hoz005.">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"7F36978A105DEDE58D71A7D8D768DDC8">
		["build_uid"] = <"b5e5525a-3324-44b1-a91d-d991d4abb394">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000.1] matches {    -- Oocyte summary
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..*} matches {    -- Biospecimen ID
						value matches {
							DV_IDENTIFIER matches {*}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003.1] occurrences matches {0..1} matches {    -- Biospecimen type
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.1,    -- Oocyte
									at0.2]    -- Ovarian tissue
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {    -- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {    -- Date of collection
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0.3] occurrences matches {0..1} matches {    -- Maturation stage
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.4,    -- Immature
									at0.5,    -- Maturing
									at0.6]    -- Mature
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Treatment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005.1] occurrences matches {0..1} matches {    -- Type of preservation
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0.7,    -- Vitrification
									at0.8,    -- Slow active freezing
									at0.9,    -- Slow passive freezing
									at0.10]    -- Not cryopreserved
								}
							}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {    -- Date of preservation
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Storage details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.biobank_storage(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.biobank_storage(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {    -- Disposition
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0008] occurrences matches {0..1} matches {    -- Date of disposition
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {    -- Expiry date
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0010] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0011] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0012] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Biospecimen ID">
					description = <"Unique identifier for the biospecimen.">
					comment = <"For example: SEC ID in Europe for assisted reproduction treatments.">
				>
				["at0003"] = <
					text = <"Biospecimen type">
					description = <"The type of cell or tissue collected.">
				>
				["at0004"] = <
					text = <"Date of collection">
					description = <"The date/time when the biospecimen was collected or harvested.">
				>
				["at0005"] = <
					text = <"Type of preservation">
					description = <"The technique used to preserve the biospecimen.">
				>
				["at0006"] = <
					text = <"Date of preservation">
					description = <"The date/time when the biospecimen was preserved.">
				>
				["at0007"] = <
					text = <"Disposition">
					description = <"The final disposition of the biospecimen.">
					comment = <"Usually this involves use, donation or disposal.">
				>
				["at0008"] = <
					text = <"Date of disposition">
					description = <"The date/time when the biospecimen was preserved.">
				>
				["at0009"] = <
					text = <"Expiry date">
					description = <"The date beyond the biospecimen is not to be used.">
				>
				["at0010"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Last updated">
					description = <"The date this biospecimen summary was last updated.">
				>
				["at0012"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0013"] = <
					text = <"Description">
					description = <"Narrative description about the biospecimen.">
					comment = <"For example, information about how it was collected.">
				>
				["at0014"] = <
					text = <"Storage details">
					description = <"">
				>
				["at0015"] = <
					text = <"Treatment">
					description = <"Narrative description about the management of the biospecimen prior to preservation, including clinical treatments.">
					comment = <"It is possible to link from this data element to an ACTION.procedure within the health record to record the specific details about the clinical procedure/s and timing.">
				>
				["at0000.1"] = <
					text = <"Oocyte summary">
					description = <"Summary or persistent information about one or more oocytes collected or harvested.">
				>
				["at0003.1"] = <
					text = <"Biospecimen type">
					description = <"The type of cell or tissue collected.">
				>
				["at0.1"] = <
					text = <"Oocyte">
					description = <"The female gamete.">
				>
				["at0.2"] = <
					text = <"Ovarian tissue">
					description = <"Fragment of the ovary.">
				>
				["at0.3"] = <
					text = <"Maturation stage">
					description = <"The stage of maturation of the oocyte/s.">
				>
				["at0.4"] = <
					text = <"Immature">
					description = <"An oocyte at prophase of meiosis I (i.e. an oocyte at the germinal vesicle (GV) stage).">
				>
				["at0.5"] = <
					text = <"Maturing">
					description = <"An oocyte that has progressed from prophase I but has not completed telophase I, thus does not exhibit the first polar body.">
				>
				["at0.6"] = <
					text = <"Mature">
					description = <"An oocyte at metaphase of meiosis II, exhibiting the first polar body and with the ability to become fertilized.">
				>
				["at0005.1"] = <
					text = <"Type of preservation">
					description = <"The technique used to preserve the biospecimen.">
				>
				["at0.7"] = <
					text = <"Vitrification">
					description = <"Biospecimen is cryopreserved using a technique that leads to a glass-like solidification.">
				>
				["at0.8"] = <
					text = <"Slow active freezing">
					description = <"Biospecimen is cryopreserved using a computerized controlled-rate freezer.">
				>
				["at0.9"] = <
					text = <"Slow passive freezing">
					description = <"Biospecimen is cryopreserved without using a computerized controlled-rate freezer.">
				>
				["at0.10"] = <
					text = <"Not cryopreserved">
					description = <"Biospecimen is fresh or refrigerated.">
				>
			>
		>
	>
	term_bindings = <
		["SNOMED-CT"] = <
			items = <
				["at0.1"] = <[SNOMED-CT::86082002]>
				["at0.2"] = <[SNOMED-CT::127482009]>
			>
		>
	>
