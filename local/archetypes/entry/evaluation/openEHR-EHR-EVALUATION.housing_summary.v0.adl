archetype (adl_version=1.4; uid=6ca63c31-f286-4712-a53b-ee83e87fe1fa)
	openEHR-EHR-EVALUATION.housing_summary.v0

concept
	[at0000]	-- Housing summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-05-29">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record summary or persistent information about an individual's current and past housing or accommodation situation.">
			use = <"Use to record summary or persistent information about an individual's current and past housing or accommodation situation.

Details about about specific housing or accommodation can be recorded using the CLUSTER.housing_record within the Housing record SLOT. This will enable flexibility to record situations where individuals live in more than one place during the same period of time, for main residences and holiday houses, etc.

The 'Last updated' data element will record the last time that the Housing summary as a whole, including individual housing records, was updated.

Use to incorporate the narrative descriptions of housing history within existing or legacy clinical systems into an archetyped format, using the 'Description' data element.">
			keywords = <"housing,accommodation,living,dwelling,house,living,arrangement", ...>
			misuse = <"Not to be used to record specific details about the setting in which an individual usually resides - use CLUSTER.housing_record nested within the Housing record SLOT in this archetype.

Not to be used to record specific details about the people with which an individual lives - use CLUSTER.living_arrangement nested within the Additional details SLOT in the CLUSTER.housing_record, or similar archetypes which are clinically appropriate.

Not to be used to record specific details about the housing structure in which an individual lives - use CLUSTER.dwelling nested within the Additional details SLOT in the CLUSTER.housing_record, or similar archetypes which are clinically appropriate.

Not to be used to record the physical address where an individual lives - use demographic archetypes for this purpose, or CLUSTER.address within the CLUSTER.housing_record, or similar, if the individual's address needs to be recorded within the health record.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Vebjørn Arntzen, Oslo University Hospital, Norway (openEHR Editor)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)", "Heather Leslie, Atomica Informatics, Australia (openEHR Editor)", "John Tore Valand, Helse Bergen, Norway (openEHR Editor)">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"DFC7C3C99BF8618E22F05766A624A8CE">
		["build_uid"] = <"093ab0c0-1355-40b6-85b8-b1a4b6723cee">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {	-- Housing summary
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Description
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0009] occurrences matches {0..*} matches {	-- Housing record
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.housing_record(-[a-zA-Z0-9_]+)*\.v0/}
					}
					allow_archetype CLUSTER[at0003] occurrences matches {0..*} matches {	-- Additional details
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0012] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {	-- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Housing summary">
					description = <"Summary or persistent information about an individual's current and past housing or accommodation situation.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Description">
					description = <"Narrative description about the overall housing situation for the individual.">
				>
				["at0003"] = <
					text = <"Additional details">
					description = <"Structured details about the current housing situation, or previous housing history, of an individual.">
				>
				["at0005"] = <
					text = <"Comment">
					description = <"Additional narrative about an individual's current housing situation, or previous housing history. not captured in other fields.">
				>
				["at0009"] = <
					text = <"Housing record">
					description = <"Structured details about the specific housing situation for this episode.">
				>
				["at0012"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0013"] = <
					text = <"Last updated">
					description = <"Date when the housing summary or associated housing records was updated.">
				>
				["at0014"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
			>
		>
	>
