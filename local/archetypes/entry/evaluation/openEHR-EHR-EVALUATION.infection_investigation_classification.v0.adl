archetype (adl_version=1.4; uid=f35324e8-4315-4783-8e65-f9f474a165a6)
	openEHR-EHR-EVALUATION.infection_investigation_classification.v0

concept
	[at0000]	-- Infectious disease investigation classification
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2022-06-25">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the final disease classification at the conclusion of an infectious disease case investigation.">
			use = <"Use to record the final disease classification at the conclusion of an infectious disease case investigation.">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"D454F34B96581F1AD76D93881CEDC3CC">
		["build_uid"] = <"93ac6050-3ccf-4086-8362-88d8a2b8bf82">
		["revision"] = <"0.0.1-alpha">
	>

definition
	EVALUATION[at0000] matches {    -- Infectious disease investigation classification
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0007] occurrences matches {0..1} matches {    -- Index disease
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Classification
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0003,    -- Unconfirmed
									at0004,    -- Confirmed
									at0005,    -- Excluded
									at0006]    -- Cannot classify/Inconclusive
								}
							}
							DV_TEXT matches {*}
						}
						name matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0024,    -- Initial classification
									at0025]    -- Final classification
								}
							}
						}
					}
					ELEMENT[at0008] occurrences matches {0..1} matches {    -- Rationale for confirmation
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {    -- Rationale for exclusion
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0011] occurrences matches {0..1} matches {    -- Infection source
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0012] occurrences matches {0..1} matches {    -- Source certainty
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0021] occurrences matches {0..1} matches {    -- Source country
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0022] occurrences matches {0..1} matches {    -- Case linkage?
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0023] occurrences matches {0..*} matches {    -- Linked contact
						value matches {
							DV_TEXT matches {*}
							DV_IDENTIFIER matches {*}
						}
					}
					allow_archetype CLUSTER[at0016] occurrences matches {0..*} matches {    -- Additional details
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.drug_resistance_profile(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.clinical_evidence\.v1/}
					}
					ELEMENT[at0017] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0013] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {    -- Last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Infectious disease investigation classification">
					description = <"Disease classification at the conclusion of an infectious disease case investigation.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Classification">
					description = <"Classification of the likelihood of the index infectious disease as cause of illness.">
					comment = <"Run-time name constraints - 'Initial classification' is the classification made prior to commencing the investigation; 'Final classification' is the conclusion reached after the investigation.">
				>
				["at0003"] = <
					text = <"Unconfirmed">
					description = <"The presence of infection by the index disease has not yet been confirmed or excluded, for example awaiting results. Also known as suspected, possible or probable.">
				>
				["at0004"] = <
					text = <"Confirmed">
					description = <"The presence of infection by the index disease has been confirmed.">
				>
				["at0005"] = <
					text = <"Excluded">
					description = <"The presence of infection by the index disease has been excluded.">
				>
				["at0006"] = <
					text = <"Cannot classify/Inconclusive">
					description = <"It is not possible to determine if infection by the index disease is confirmed or excluded.">
				>
				["at0007"] = <
					text = <"Index disease">
					description = <"The name of the infectious disease under investigation.">
					comment = <"This data element may be redundant if the index disease has been identified elsewhere within a template.">
				>
				["at0008"] = <
					text = <"Rationale for confirmation">
					description = <"The rationale or evidence for confirming a case as causal.">
					comment = <"For example: clinical diagnosis; laboratory test result; or epidemiological link.">
				>
				["at0009"] = <
					text = <"Rationale for exclusion">
					description = <"The rationale or evidence for exclusion or discarding of a case as not causal.">
					comment = <"For example: laboratory test result; or alternate diagnosis made.">
				>
				["at0011"] = <
					text = <"Infection source">
					description = <"The most likely category for the source of infection.">
					comment = <"For example: imported or travel-related; direct contact with a family member; vertical transmission; or unknown.">
				>
				["at0012"] = <
					text = <"Source certainty">
					description = <"The likelihood or level of certainty that the identified source is the cause of infection.">
					comment = <"For example: suspected; probable; confirmed.">
				>
				["at0013"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0015"] = <
					text = <"Last updated">
					description = <"The date when this infectious disease investigation classification was last updated.">
				>
				["at0016"] = <
					text = <"Additional details">
					description = <"">
				>
				["at0017"] = <
					text = <"Comment">
					description = <"Additional narrative about the classification, not captured in other fields.">
				>
				["at0021"] = <
					text = <"Source country">
					description = <"Country identified as the (likely or probable) source of the infection.">
				>
				["at0022"] = <
					text = <"Case linkage?">
					description = <"Was this case linked to another case?">
				>
				["at0023"] = <
					text = <"Linked contact">
					description = <"">
				>
				["at0024"] = <
					text = <"Initial classification">
					description = <"">
				>
				["at0025"] = <
					text = <"Final classification">
					description = <"">
				>
			>
		>
	>
